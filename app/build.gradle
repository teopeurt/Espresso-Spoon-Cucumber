apply plugin: 'com.android.application'


android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.emmasuzuki.cucumberespressodemo"
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"

        testApplicationId "com.emmasuzuki.cucumberespressodemo.test"
        testInstrumentationRunner "com.emmasuzuki.cucumberespressodemo.test.Instrumentation"
    }

    sourceSets {
        androidTest {
            assets.srcDirs = ['src/androidTest/assets']
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }

    lintOptions {
        abortOnError false
    }
}

apply plugin: 'spoon'

spoon {

    // Enable debug output
    debug = true

    // Set custom test reports directory (defaults to "spoon")
    baseOutputDir = file("$buildDir/custom-report-dir")

    // Enable setting test class/method-to-be-run from command line. E.g.:
    // $> ../gradlew spoonFreeDebugTest -PspoonClassName=com.stanfy.spoon.example.test.MainActivityTest -PspoonMethodName=testSetText
    if (project.hasProperty('spoonClassName')) {
        className = project.spoonClassName

        if (project.hasProperty('spoonMethodName')) {
            methodName = project.spoonMethodName
        }
    }

    // You can pass instrumentation arguments. Tests in this example assert this pair is passed.
    instrumentationArgs = ["foo=bar"]

    // It's also possible to configure tests sharding using convenient properties:
    // numShards = 2
    // shardIndex = 0

    adbTimeout = 30

    codeCoverage = true
}

dependencies {
    compile 'com.android.support:appcompat-v7:22.0.0'

    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.0'
    androidTestCompile 'com.android.support.test:testing-support-lib:0.1'
    androidTestCompile 'info.cukes:cucumber-android:1.2.0@jar'
    androidTestCompile 'info.cukes:cucumber-picocontainer:1.2.0'

    androidTestCompile "com.squareup.spoon:spoon-client:1.5.0"
}
